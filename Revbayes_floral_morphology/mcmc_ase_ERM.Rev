###########################################################################################################
# Ancestral State reconstruction and stochastic character mapping under an equal rates Markov (ERM) model #
###########################################################################################################

CHARACTER = "syndromes"
NUM_STATES = 3

# Import the morphological character matrix
morpho <- readDiscreteCharacterData("data/"+CHARACTER+".nex")

# Create some vector for the moves and monitors of this analysis
moves    = VectorMoves()
monitors = VectorMonitors()



##############
# Tree model #
##############

phylogeny <- readTrees("data/Hillieae_DTE_fixed_topology.tree")[1]


#########################################
# Set up the rate model and rate matrix #
#########################################

# We assume every rate i <-> j to be exponentially distributed
rate_pr := phylogeny.treeLength() / 10
mu ~ dnExp(rate_pr)
moves.append( mvScale( mu, weight=2 ) )

# Specify the rate matrix
NUM_RATES = NUM_STATES * (NUM_STATES-1)
for ( i in 1:NUM_RATES ) {
    rate[i] := mu
}


Q_morpho := fnFreeK( rate, rescale=false )



#####################################
# Set up the root state frequencies #
#####################################

# Specify equal priors at the root
rf_prior <- rep(1,NUM_STATES)
rf <- simplex( rf_prior )


###################
# Set up the CTMC #
###################

# Combine all of our elements into a CTMC.

phyMorpho ~ dnPhyloCTMC(tree=phylogeny, Q=Q_morpho, rootFrequencies=rf, type="Standard")
phyMorpho.clamp(morpho)



########
# MCMC #
########

# Initialize the model object 
mymodel = model(phylogeny)

# Create a vector of monitors 
## 1. for the full model #
monitors.append( mnModel(filename="output/"+CHARACTER+"_ERM.log", printgen=1) )
## 2. and a few select parameters to be printed to the screen #
monitors.append( mnScreen(printgen=100) )
## 3. add an ancestral state monitor
monitors.append( mnJointConditionalAncestralState(tree=phylogeny,
                                                  ctmc=phyMorpho,
                                                  filename="output/"+CHARACTER+"_ERM.states.txt",
                                                  type="Standard",
                                                  printgen=1,
                                                  withTips=true,
                                                  withStartStates=false) )
## 4. add stochastic character map monitor 
monitors.append( mnStochasticCharacterMap(ctmc=phyMorpho, filename="output/"+CHARACTER+"_ERM.maps.log", printgen=1, include_simmap=true) )

# Initialize the MCMC object
mymcmc = mcmc(mymodel, monitors, moves, nruns=2, combine="mixed")

# Run the MCMC
mymcmc.run(generations=10000, tuningInterval=200)


# Read in the tree trace and construct the ancestral states (ASE) #
anc_states = readAncestralStateTrace("output/"+CHARACTER+"_ERM.states.txt")
anc_tree = ancestralStateTree(tree=phylogeny, ancestral_state_trace_vector=anc_states, include_start_states=false, file="output/"+CHARACTER+"_ase_ERM.tree", burnin=0.25, summary_statistic="MAP", site=1)
anc_state_trace = readAncestralStateTrace("output/"+CHARACTER+"_ERM.maps.log")
characterMapTree(phylogeny, anc_state_trace, character_file="output/character.tree", posterior_file="output/posterior.tree", burnin=5, reconstruction="marginal")

# Quit RevBayes
q()
